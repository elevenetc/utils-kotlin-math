plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {

    repositories {
        maven {
            name = "MavenCentral"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            credentials {
                username = getProp('MAVEN_UPLOAD_USER')
                password = getProp('MAVEN_UPLOAD_PWD')
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId "com.elevenetc"
            artifactId "utils.kotlin.math"
            version '0.1.0'

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Kotlin math utils"
                description = "Set of math util classes and functions in Kotlin"
                url = "https://github.com/elevenetc/utils-kotlin-math"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "com.elevenetc"
                        name = "Eugene Levenetc"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/elevenetc/utils-kotlin-math.git"
                    developerConnection = "scm:git:https://github.com/elevenetc/utils-kotlin-math.git"
                    url = "https://github.com/elevenetc/utils-kotlin-math.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    useInMemoryPgpKeys(getProp('PGP_SIGNING_KEY'), getProp('PGP_SIGNING_PASSWORD'))
}

def getProp(name) {
    if (project.hasProperty(name)) return project.property(name)
    else return ""
}